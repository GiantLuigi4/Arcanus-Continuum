plugins {
	id 'maven-publish'
	alias libs.plugins.quilt.loom
}

archivesBaseName = project.archives_base_name
version = project.version
group = project.maven_group

//FIXME workaround for quilt-loom bug
project.configurations.configureEach {
	exclude(group: "net.fabricmc", module: "fabric-loader")
	exclude(group: "net.fabricmc.fabric-api")
}

repositories {
	maven { url "https://maven.quiltmc.org/repository/snapshot" }
	maven { url "https://maven.shedaniel.me" }
	maven { url "https://maven.terraformersmc.com" }
	maven { url "https://maven.ladysnake.org/releases" }
	maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
	maven { url "https://maven.blamejared.com" }
	maven { url "https://maven.kosmx.dev/" }
	maven { url "https://api.modrinth.com/maven" }
	maven { url "https://cursemaven.com" }
	maven { url "https://minecraft.curseforge.com/api/maven/" }
	maven { url "https://maven.jamieswhiteshirt.com/libs-release" }
	maven { url "https://jitpack.io" }
}

dependencies {
	minecraft libs.minecraft

	mappings variantOf(libs.quilt.mappings) { classifier "intermediary-v2" }

	modImplementation libs.quilt.loader
	modImplementation libs.quilted.fabric.api
	modImplementation libs.cca.base
	modImplementation libs.cca.entity
	modImplementation libs.patchouli
	modImplementation libs.midnightlib
	modImplementation libs.reach.attr
	modImplementation libs.modmenu
	modImplementation libs.smartbrainlib

	modImplementation libs.firstperson
	modImplementation libs.pehkui
	modImplementation libs.kanos

	implementation libs.joml
	implementation libs.mixinextras
	annotationProcessor libs.mixinextras

	include libs.joml
	include libs.mixinextras
	include libs.reach.attr
}

loom {
	accessWidenerPath = file("src/main/resources/arcanuscontinuum.accesswidener")
}

processResources {
	inputs.property "version", version

	filesMatching('quilt.mod.json') {
		expand "version": version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Still required by IDEs such as Eclipse and Visual Studio Code
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17

	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	// If this mod is going to be a library, then it should also generate Javadocs in order to aid with development.
	// Uncomment this line to generate them.
	// withJavadocJar()
}

// If you plan to use a different file for the license, don't forget to change the file name here!
jar {
	from("LICENSE.md") {
		rename { "LICENSE_${archivesBaseName}.md" }
	}
}

// Configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
